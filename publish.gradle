// must be in build.gradle: plugins { id "com.jfrog.bintray" version "1.8.4" } 
apply plugin: 'maven-publish'

// -------------- Maven POM --------------

def pomConfig = {
    licenses {
        license {
            name "The Apache Software License, Version 2.0"
            url "http://www.apache.org/licenses/LICENSE-2.0.txt"
            distribution "repo"
        }
    }
    developers {
        developer {
            id "esoco"
            name "esoco GmbH"
            email "info@esoco.de"
        }
    }
    
    scm {
       url "https://github.com/esoco/${project.name}"
    }
}

publishing {
    publications {
        Maven(MavenPublication) {
            from components.java
            afterEvaluate {
	            artifact sourcesJar
	            artifact javadocJar
	            groupId project.group
	            artifactId project.name
	            version project.version
	            pom.withXml {
	                def root = asNode()
	                root.appendNode('description', project.description)
	                root.appendNode('name', project.name)
	                root.appendNode('url', "https://github.com/esoco/${project.name}")
	                root.children().last() + pomConfig
	            }
            }
        }
    }
}

// ----------- Bintray Release -----------

bintray {
	user = project.hasProperty('bintrayUser') ?	
			   project.property('bintrayUser') :
			   System.getenv('BINTRAY_USER')
	key = project.hasProperty('bintrayApiKey') ? 
			  project.property('bintrayApiKey') :
			  System.getenv('BINTRAY_API_KEY')
    
    configurations = ['archives']
    publish = true

	pkg {
		repo = 'sdack'
		name = project.name
		version {
			name = project.version
            vcsTag = project.version
            gpg {
                sign = true
            }
            mavenCentralSync {
                sync = true
                user = project.hasProperty('ossUser') ?	
                		   project.property('ossUser') : 
                		   System.getenv('OSS_USER')
                password = project.hasProperty('ossPassword') ?	
                		       project.property('ossPassword') : 
                		       System.getenv('OSS_PASSWORD')
            }            
        }
	    
    }
}
