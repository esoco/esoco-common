plugins {
    id "java-library"
    id "maven-publish"
    id "pl.allegro.tech.build.axion-release" version "$axionReleaseVersion"
    id "io.github.gradle-nexus.publish-plugin" version "$nexusPublishVersion"
}

repositories {
	mavenLocal()
	mavenCentral()
}

scmVersion { tag { prefix.set("") }}

project.version = scmVersion.version
project.group = 'de.esoco'
project.description = 'Common ty for the esoco framework stack'


sourceCompatibility = 1.8

dependencies {
    implementation "jakarta.validation:jakarta.validation-api:$jakartaValidationVersion"
    
	testImplementation platform("org.junit:junit-bom:$junitVersion")
	testImplementation 'org.junit.jupiter:junit-jupiter'
}

// sources are needed for the GWT compiler
jar { from sourceSets.main.allSource }

task sourcesJar(type: Jar, dependsOn: classes) {
    classifier = 'sources'
    from sourceSets.main.allSource
}

task javadocJar(type: Jar, dependsOn: javadoc) {
    classifier = 'javadoc'
    from javadoc.destinationDir
}

artifacts {
    archives sourcesJar
    archives javadocJar
}

tasks.withType(Javadoc) {
    failOnError = false
    options.addBooleanOption('quiet', true)
    
    if (JavaVersion.current().isJava8Compatible()) {
        options.addBooleanOption('Xdoclint:none', true) 
    }
}

publishing {
    publications {
        mavenJava(MavenPublication) {
            from(components.java)
        }
    }
}

nexusPublishing {
    repositories {
        sonatype {  //only for users registered in Sonatype after 24 Feb 2021
            nexusUrl.set(uri("https://s01.oss.sonatype.org/service/local/"))
            snapshotRepositoryUrl.set(uri("https://s01.oss.sonatype.org/content/repositories/snapshots/"))
        }
    }
}